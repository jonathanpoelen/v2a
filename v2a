#!/usr/bin/env zsh
set -e

typeset -i inplace=0
if [[ $1 = -i ]]; then
  inplace=1
  shift
fi

if (( $# < 1 )) ; then
  echo -E 'Convert a video file to an audio file

usage: v2a infile [outfile|directory] [ffmpeg options] ...
usage: v2a -i infile [ffmpeg options] ...'
  exit 1
fi

input=$1
if (( inplace == 0 )); then
  if [[ -z $2 ]] || [[ ${2[1]} = '-' ]]; then
    output=$input
    shift
  elif [[ -d $2 ]]; then
    output=$2/${1:t}
    shift 2
  else
    output=$2
    shift 2
  fi

  if [[ $input -ef $output ]]; then
    echo -n "Edit file in-place? [y/n] "
    read -q || exit 2
    inplace=1
  fi
else
  shift
fi

if (( inplace == 1 )); then
  output=$input.$RANDOM-$$.mp4
  ffmpeg -i $input $@ -vn $output
  mv $output $input
else
  ffmpeg -i $input $@ -vn $output
fi
